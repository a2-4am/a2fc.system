!cpu 6502
!to "../build/A2FC.SYSTEM#FF2000",plain
*=$2000
;
; A2FC.SYSTEM
; (c) 2018 by 4am
; a very small DHGR graphics viewer
; takes pathname of .a2fc file (in $2006),
; loads it,
; displays it,
; waits for key,
; quits via MLI
;

;
; MLI commands
;
kMLIOpen = $C8
kMLIRead = $CA
kMLIClose = $CC
kMLIQuit = $65
;
; MLI parameter counts
;
kMLIOpenCount = $03
kMLIReadCount = $04
kMLICloseCount = $01
kMLIQuitCount = $04
;
; other stuff
;
aFileBuffer = $9200
MLI = $BF00

         jmp   Start      ; magic jump
         !byte $EE,$EE    ; magic bytes
         !byte $40        ; length of inputfile buffer
inputfilebuffer
         !fill $40
Start
         ldx   #(CodeEnd-CodeStart)-1
-        lda   CodeStart,x
         sta  $00fe,x
         dex
         bpl   -
         txs
         jmp   MLI

CodeStart
!pseudopc $fe {
         !word $2000
         !word Open-1
Open
         !byte kMLIOpen
         !word mliParamForOpen
         bcs   Quit
         jsr   Read1
         bcs   Close
;DHGRCopy
         sta   $C00A
         sta   $C000
         sta   $C005                 ; read from mainmem, write to auxmem
-        lda   ($ff,x)
         sta   ($ff,x)
         inc   $fe
         bne   -
         inc   $ff
         bit   $ff
         bvc   -
         sta   $C004                 ; read/write from mainmem
         jsr   Read2
Close
         php
         jsr   MLI
         !byte kMLIClose
         !word mliParamForClose
         plp
         bcs   Quit
;DHGRShow
         lda   $C05E
         sta   $C00D
         bit   $C050
         bit   $C054
         bit   $C052
         bit   $C057
;WaitForKey
-        lda   $C000
         bpl   -
Quit
         lsr   mlicmd
Read1
         ldy   #kMLIReadCount        ; also #kMLIQuitCount
         sty   mliparam
Read2
         jsr   MLI
mlicmd   !byte kMLIRead
         !word mliparam
         rts

mliParamForClose
         !byte kMLICloseCount
         !byte 0                     ; all handles

mliParamForOpen
         !byte kMLIOpenCount
         !word inputfilebuffer
         !byte <aFileBuffer
mliparam
         !byte >aFileBuffer
         !byte $00                   ; ProDOS file refnum (filled by MLI_OPEN call)
         !word $2000                 ; data address
         !word $2000                 ; data length
}
CodeEnd
